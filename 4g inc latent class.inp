TITLE: Inclusive PCD latent class model

DATA:
	FILE = applied.dat;

VARIABLE:  
    NAMES = aln female rs_soc cd1 cd2 cd3 cd4 cd5 cd6 cd_modclass drugs_18 int_18 qlet;
	USEVARIABLES = female rs_soc cd1 cd2 cd3 cd4 cd5 cd6 drugs_18 int_18;
	CATEGORICAL = cd1-cd6;
	CLASSES = class(4);
    Missing are all (-9999) ;
    USEOBSERVATIONS = drugs_18 NE -9999 AND int_18 NE -9999 AND cd_modclass NE -9999;
	
ANALYSIS: 
    TYPE = MIXTURE;
    ESTIMATOR = MLR;	
	STARTS = 500 50;
	PROC = 4(STARTS);
	
MODEL:
	%OVERALL%
    class#1 on female rs_soc drugs_18 int_18;
    class#2 on female rs_soc drugs_18 int_18;
    class#3 on female rs_soc drugs_18 int_18;

    %class#1%
    [cd1$1 cd2$1 cd3$1 cd4$1 cd5$1 cd6$1] (c11-c16);

	%class#2%
    [cd1$1 cd2$1 cd3$1 cd4$1 cd5$1 cd6$1] (c21-c26);

    %class#3%
    [cd1$1 cd2$1 cd3$1 cd4$1 cd5$1 cd6$1] (c31-c36);

    %class#4%
    [cd1$1 cd2$1 cd3$1 cd4$1 cd5$1 cd6$1] (c41-c46);	

	model constraint:
    !deriving AUC to work out class order
    NEW(auc1 auc2 auc3 auc4);
    auc1 = exp(-c11)/(1+exp(-c11))
        + 2*exp(-c12)/(1+exp(-c12))
        + 3*exp(-c13)/(1+exp(-c13))
        + 4*exp(-c14)/(1+exp(-c14))
        + 5*exp(-c15)/(1+exp(-c15))
        + 6*exp(-c16)/(1+exp(-c16));
    auc2 = exp(-c21)/(1+exp(-c21))
        + 2*exp(-c22)/(1+exp(-c22))
        + 3*exp(-c23)/(1+exp(-c23))
        + 4*exp(-c24)/(1+exp(-c24))
        + 5*exp(-c25)/(1+exp(-c25))
        + 6*exp(-c26)/(1+exp(-c26));
    auc3 = exp(-c31)/(1+exp(-c31))
        + 2*exp(-c32)/(1+exp(-c32))
        + 3*exp(-c33)/(1+exp(-c33))
        + 4*exp(-c34)/(1+exp(-c34))
        + 5*exp(-c35)/(1+exp(-c35))
        + 6*exp(-c36)/(1+exp(-c36));
    auc4 = exp(-c41)/(1+exp(-c41))
        + 2*exp(-c42)/(1+exp(-c42))
        + 3*exp(-c43)/(1+exp(-c43))
        + 4*exp(-c44)/(1+exp(-c44))
        + 5*exp(-c45)/(1+exp(-c45))
        + 6*exp(-c46)/(1+exp(-c46));

SAVEDATA:
    FILE = inc.txt;
	SAVE = cprob;